// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
        maven { url "https://jetbrains.bintray.com/intellij-third-party-dependencies" }
        jcenter()
    }
    dependencies {
        classpath libs.gradle
        classpath 'com.google.gms:google-services:4.3.15'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.2.2'
        classpath libs.kotlin.gradle
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.5'
        classpath libs.dagger.hilt.gradle
        classpath libs.sqldelight.gradle
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven {
            url "https://jitpack.io"
            content {
                includeGroup 'com.github.deano2390'
            }
        }
    }
}

subprojects {
    if (name == 'app') {
        apply plugin: 'com.android.application'
        apply plugin: 'kotlin-android'
        apply plugin: 'kotlin-kapt'
        apply from: "$rootDir/jacoco.gradle"
    } else if (name == 'benchmark') {
        apply plugin: 'com.android.test'
        apply plugin: 'kotlin-android'
    } else {
        apply plugin: 'com.android.library'
        apply plugin: 'kotlin-android'
        apply plugin: 'kotlin-kapt'
        apply from: "$rootDir/jacoco.gradle"
    }

    android {
        compileSdkVersion 33

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 33
            versionName "1.10.0"
            versionCode 60
        }

        if (name != 'benchmark') {
            buildFeatures {
                compose true
            }

            composeOptions {
                kotlinCompilerExtensionVersion libs.versions.jetpack.compose.compiler.get()
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_17
            targetCompatibility JavaVersion.VERSION_17
        }

        kotlinOptions {
            jvmTarget = '17'
        }

        testOptions {
            unitTests.returnDefaultValues = true
        }

        lintOptions {
            // Bug in Gradle plugin 4.0.0
            disable 'RequiredSize'
        }
    }

    if (name != 'benchmark') {
        dependencies {
            def composeBom = platform(libs.compose.bom)
            implementation composeBom
            implementation 'androidx.compose.material:material'
            implementation 'androidx.compose.foundation:foundation'
            implementation 'androidx.compose.ui:ui'
            implementation 'androidx.compose.ui:ui-tooling-preview'
            debugImplementation 'androidx.compose.ui:ui-tooling'
            implementation 'androidx.compose.material:material-icons-core'
            implementation 'androidx.compose.material:material-icons-extended'
            implementation 'androidx.compose.runtime:runtime-livedata'

            implementation 'androidx.activity:activity-compose:1.7.1'
            implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1'

            testImplementation 'junit:junit:4.13.2'
            testImplementation 'org.mockito.kotlin:mockito-kotlin:4.1.0'
            testImplementation libs.assertj
        }
    }
}

