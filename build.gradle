// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
        maven { url "https://jetbrains.bintray.com/intellij-third-party-dependencies" }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath 'com.google.gms:google-services:4.3.15'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.2.2'
        classpath libs.kotlin.gradle
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.2'
        classpath libs.dagger.hilt.gradle
        classpath libs.sqldelight.gradle
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        jcenter()
        maven {
            url "https://jitpack.io"
            content {
                includeGroup 'com.github.deano2390'
            }
        }
    }
}

subprojects {
    if (name == 'app') {
        apply plugin: 'com.android.application'
        apply plugin: 'kotlin-android'
        apply plugin: 'kotlin-kapt'
        apply from: "$rootDir/jacoco.gradle"
    } else if (name == 'benchmark') {
        apply plugin: 'com.android.test'
        apply plugin: 'kotlin-android'
    } else {
        apply plugin: 'com.android.library'
        apply plugin: 'kotlin-android'
        apply plugin: 'kotlin-kapt'
        apply from: "$rootDir/jacoco.gradle"
    }

    android {
        compileSdkVersion 33

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 33
            versionName "1.9.0"
            versionCode 59

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        if (name != 'benchmark') {
            buildFeatures {
                compose true
            }

            composeOptions {
                kotlinCompilerExtensionVersion libs.versions.jetpack.compose.compiler.get()
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }

        kotlinOptions {
            jvmTarget = '11'
        }

        testOptions {
            unitTests.returnDefaultValues = true
        }

        lintOptions {
            // Bug in Gradle plugin 4.0.0
            disable 'RequiredSize'
        }
    }

    if (name != 'benchmark') {
        dependencies {
            def composeBom = platform(libs.compose.bom)
            implementation composeBom
            implementation 'androidx.compose.material:material'
            implementation 'androidx.compose.foundation:foundation'
            implementation 'androidx.compose.ui:ui'
            implementation 'androidx.compose.ui:ui-tooling-preview'
            debugImplementation 'androidx.compose.ui:ui-tooling'
            implementation 'androidx.compose.material:material-icons-core'
            implementation 'androidx.compose.material:material-icons-extended'
            implementation 'androidx.compose.runtime:runtime-livedata'

            implementation 'androidx.activity:activity-compose:1.6.1'
            implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'

            testImplementation 'junit:junit:4.13.2'
            testImplementation 'org.mockito.kotlin:mockito-kotlin:4.1.0'
            testImplementation libs.assertj
        }
    }
}


apply plugin: 'jacoco'

jacoco {
    toolVersion = libs.versions.jacoco.get()
}

task jacocoMerge(type: JacocoMerge) {
    gradle.afterProject { p, state ->
        if (p.rootProject != p && p.plugins.hasPlugin('jacoco')) {
            executionData file("${p.buildDir}/jacoco").listFiles().findAll {
                it.name.endsWith(".exec")
            }
        }
    }
}

def coverageExcludeFiles = ['**/R.class', '**/R$*.class', '**/com/android/**/*.*',
                            '**/BuildConfig.class', '**/*Activity*.class',
                            '**/*Fragment*.class', '**/*Receiver.class',
                            '**/*Manifest*.class', '**/*Application*.class',
                            '**/com/phicdy/mycuration/data/db/*.*',
                            '**/com/phicdy/mycuration/data/repository/*.*',
                            '**/com/phicdy/mycuration/data/network/*',
                            '**/com/phicdy/mycuration/di/*.*',
                            '**/com/phicdy/mycuration/domain/alarm/*.*',
                            '**/com/phicdy/mycuration/domain/entity/Filter*',
                            '**/com/phicdy/mycuration/domain/entity/Article.class',
                            '**/com/phicdy/mycuration/domain/entity/Curation.class',
                            '**/com/phicdy/mycuration/domain/entity/CurationCondition.class',
                            '**/com/phicdy/mycuration/domain/entity/CurationSelection.class',
                            '**/com/phicdy/mycuration/domain/entity/Feed*',
                            '**/com/phicdy/mycuration/domain/task/GetFeedIconTask*',
                            '**/com/phicdy/mycuration/presentation/view/*',
                            '**/com/phicdy/mycuration/util/log/*',
                            '**/com/phicdy/mycuration/tracker/*.*']
task jacocoMergedReport(type: JacocoReport, dependsOn: [tasks.jacocoMerge]) {
    executionData jacocoMerge.destinationFile
    def sources = []
    subprojects.forEach {
        sources += "${it.projectDir.path}/src/main/java"
    }
    getSourceDirectories().setFrom(files(sources))
    getClassDirectories().setFrom(
            fileTree(
                dir: ".",
                includes: ["**/build/intermediates/classes/debug/**",
                           "**/build/tmp/kotlin-classes/debug/**"],
                excludes: coverageExcludeFiles
            )
    )

    reports {
        xml.enabled = true
        html.enabled true
        csv.enabled false
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.destination file("${buildDir}/reports/jacoco/html")
    }
}

task testDebugUnitTest(dependsOn: [tasks.jacocoMergedReport])
