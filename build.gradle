// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = '1.3.50'
    ext.timber_version = '4.7.1'
    ext.coroutine_version = '1.3.0'
    ext.lifecycle_version = '2.2.0-rc01'
    ext.arch_core_testing_version = '2.1.0'
    ext.appcompat_version = '1.1.0-beta01'
    ext.recyclerview_version = '1.1.0-alpha05'
    ext.constraintlayout_version = '2.0.0-beta1'
    ext.material_version = '1.1.0-alpha07'
    ext.retrofit_version = '2.6.0'
    ext.rxjava_version = '2.2.0'
    ext.rxandroid_version = '2.1.0'
    ext.koin_version = '2.0.1'
    ext.firebase_version = '16.0.9'
    ext.jacoco_version = "0.8.2"
    ext.glideVer = '4.8.0'
    ext.work_version = '2.2.0'

    repositories {
        google().content {
            includeGroupByRegex 'com\\.google.*'
            includeGroupByRegex 'com\\.android\\.tools.*'
            includeGroup 'androidx.databinding'
            includeGroup 'com.android.databinding'
            includeGroup 'com.squareup'
        }
        jcenter {
            content {
                includeGroupByRegex 'net\\.sf.*'
                includeGroupByRegex 'com\\.squareup.*'
                includeGroupByRegex 'com\\.googlecode.*'
                includeGroupByRegex 'com\\.google.*'
                includeGroupByRegex 'org\\.apache.*'
                includeGroupByRegex 'org\\.jetbrains.*'
                includeGroupByRegex 'org\\.ow2.*'
                includeGroupByRegex 'commons-.*'
                includeGroupByRegex 'javax.*'
                includeGroupByRegex 'com\\.sun.*'
                includeGroup 'commons-validator'
                includeGroup 'commons-logging'
                includeGroup 'commons-codec'
                includeGroup 'com.stanfy.spoon'
                includeGroup 'com.github.spullara.mustache.java'
                includeGroup 'com.beust'
                includeGroup 'com.madgag'
                includeGroup 'de.undercouch'
                includeGroup 'it.unimi.dsi'
                includeGroup 'junit'
                includeGroup 'net.java'
                includeGroup 'org.lesscss'
                includeGroup 'org.mozilla'
                includeGroup 'org.glassfish.jaxb'
                includeGroup 'org.codehaus.plexus'
                includeGroup 'org.jvnet.staxex'
                includeGroup 'org.codehaus.mojo'
                includeGroup 'org.sonatype.oss'
                includeGroup 'org.jdom'
                includeGroup 'org.jacoco'
                includeGroup 'org.antlr'
                includeGroup 'org.codehaus'
                includeGroup 'org.checkerframework'
                includeGroup 'org.bouncycastle'
                includeGroup 'oro'
                includeGroup 'xerces'
                includeGroup 'xml-apis'
            }
        }
        maven {
            url 'https://maven.fabric.io/public'
            content {
                includeGroup 'io.fabric.tools'
            }
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.2'
        classpath 'com.google.gms:google-services:4.2.0'
        classpath 'com.stanfy.spoon:spoon-gradle-plugin:1.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'io.fabric.tools:gradle:1.29.0'
    }
}

allprojects {
    repositories {
        google().content {
            includeGroupByRegex 'androidx.*'
            includeGroupByRegex 'com\\.google\\.android.*'
            includeGroupByRegex 'com\\.android\\.tools.*'
            includeGroup 'com.crashlytics.sdk.android'
            includeGroup 'com.google.firebase'
            includeGroup 'io.fabric.sdk.android'
            includeGroup 'com.android.tools.lint'
        }
        jcenter {
            content {
                includeGroupByRegex 'org\\.jetbrains.*'
                includeGroupByRegex 'com\\.squareup.*'
                includeGroupByRegex 'com\\.sun.*'
                includeGroupByRegex 'net\\.sf.*'
                includeGroupByRegex 'com\\.google.*'
                includeGroupByRegex 'org\\.apache.*'
                includeGroupByRegex 'org\\.codehaus.*'
                includeGroupByRegex 'org\\.ow2.*'
                includeGroupByRegex 'commons-.*'
                includeGroupByRegex 'javax.*'
                includeGroup 'com.jakewharton.retrofit'
                includeGroup 'com.jakewharton.timber'
                includeGroup 'com.facebook.stetho'
                includeGroup 'com.squareup.okhttp3'
                includeGroup 'com.github.bumptech.glide'
                includeGroup 'com.nhaarman.mockitokotlin2'
                includeGroup 'io.github.inflationx'
                includeGroup 'junit'
                includeGroup 'it.unimi.dsi'
                includeGroup 'net.sf.kxml'
                includeGroup 'net.bytebuddy'
                includeGroup 'org.jsoup'
                includeGroup 'org.sonatype.oss'
                includeGroup 'org.koin'
                includeGroup 'org.junit'
                includeGroup 'org.jvnet.staxex'
                includeGroup 'org.checkerframework'
                includeGroup 'org.reactivestreams'
                includeGroup 'org.hamcrest'
                includeGroup 'org.jacoco'
                includeGroup 'org.glassfish.jaxb'
                includeGroup 'org.assertj'
                includeGroup 'org.mockito'
                includeGroup 'org.bouncycastle'
                includeGroup 'org.objenesis'
            }
        }
        maven {
            url "https://jitpack.io"
            content {
                includeGroup 'com.github.deano2390'
            }
        }
    }
}

subprojects {
    if (name == 'app') {
        apply plugin: 'com.android.application'
    } else {
        apply plugin: 'com.android.library'
    }
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-kapt'
    apply from: "$rootDir/jacoco.gradle"

    android {
        compileSdkVersion 28

        defaultConfig {
            minSdkVersion 21
            targetSdkVersion 28
            versionName "1.8.2"
            versionCode 55

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        kotlinOptions {
            jvmTarget = '1.8'
        }

        testOptions {
            unitTests.returnDefaultValues = true
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

        testImplementation 'junit:junit:4.12'
        testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.0.0'
        testImplementation 'org.assertj:assertj-core:3.11.1'
    }
}


apply plugin: 'jacoco'

jacoco {
    toolVersion = jacoco_version
}

task jacocoMerge(type: JacocoMerge) {
    gradle.afterProject { p, state ->
        if (p.rootProject != p && p.plugins.hasPlugin('jacoco')) {
            executionData file("${p.buildDir}/jacoco").listFiles().findAll {
                it.name.endsWith(".exec")
            }
        }
    }
}

def coverageExcludeFiles = ['**/R.class', '**/R$*.class', '**/com/android/**/*.*',
                            '**/BuildConfig.class', '**/*Activity*.class',
                            '**/*Fragment*.class', '**/*Receiver.class',
                            '**/*Manifest*.class', '**/*Application*.class',
                            '**/com/phicdy/mycuration/data/db/*.*',
                            '**/com/phicdy/mycuration/data/repository/*.*',
                            '**/com/phicdy/mycuration/data/network/*',
                            '**/com/phicdy/mycuration/di/*.*',
                            '**/com/phicdy/mycuration/domain/alarm/*.*',
                            '**/com/phicdy/mycuration/domain/entity/Filter*',
                            '**/com/phicdy/mycuration/domain/entity/Article.class',
                            '**/com/phicdy/mycuration/domain/entity/Curation.class',
                            '**/com/phicdy/mycuration/domain/entity/CurationCondition.class',
                            '**/com/phicdy/mycuration/domain/entity/CurationSelection.class',
                            '**/com/phicdy/mycuration/domain/entity/Feed*',
                            '**/com/phicdy/mycuration/domain/task/GetFeedIconTask*',
                            '**/com/phicdy/mycuration/presentation/view/*',
                            '**/com/phicdy/mycuration/util/log/*',
                            '**/com/phicdy/mycuration/tracker/*.*']
task jacocoMergedReport(type: JacocoReport, dependsOn: [tasks.jacocoMerge]) {
    executionData jacocoMerge.destinationFile
    def sources = []
    subprojects.forEach {
        sources += "${it.projectDir.path}/src/main/java"
    }
    sourceDirectories = files(sources)
    classDirectories = fileTree(
            dir: ".",
            includes: ["**/build/intermediates/classes/debug/**",
                       "**/build/tmp/kotlin-classes/debug/**"],
            excludes: coverageExcludeFiles
    )

    reports {
        xml.enabled = true
        html.enabled true
        csv.enabled false
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.destination file("${buildDir}/reports/jacoco/html")
    }
}

task testDebugUnitTest(dependsOn: [tasks.jacocoMergedReport])
