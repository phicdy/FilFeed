import com.android.build.api.dsl.ManagedVirtualDevice

android {
    namespace 'com.phicdy.mycuration.benchmark'

    defaultConfig {
        minSdkVersion 28
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        // This benchmark buildType is used for benchmarking, and should function like your
        // release build (for example, with minification on). It's signed with a debug key
        // for easy local/CI testing.
        benchmark {
            debuggable = true
            signingConfig = debug.signingConfig
            matchingFallbacks = ["release"]
        }
    }
    testOptions {
        managedDevices {
            devices {
                pixel6Api31(ManagedVirtualDevice) {
                    device = "Pixel 6"
                    apiLevel = 33
                    systemImageSource = "aosp"
                }
            }
        }
    }

    targetProjectPath = ":app"
    experimentalProperties["android.experimental.self-instrumenting"] = true
}

dependencies {
    implementation libs.androidx.test
    implementation libs.androidx.test.core
    implementation libs.androidx.test.rules
    implementation libs.androidx.test.runner
    implementation libs.espresso
    implementation libs.uiautomator
    implementation libs.macrobenchmark
    implementation libs.junit
}

androidComponents {
    beforeVariants(selector().all()) {
        enabled = buildType == "benchmark"
    }
}